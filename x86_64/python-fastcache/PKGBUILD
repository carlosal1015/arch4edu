# Maintainer: Guillaume Horel <guillaume.horel@gmail.com>
pkgname=('python-fastcache')
_pkgname='fastcache'
pkgver='1.1.0'
pkgrel=2
pkgdesc="C implementation of Python 3 functools.lru_cache."
url="https://github.com/pbrady/fastcache"
checkdepends=('python-pytest')
depends=('python')
makedepends=('python-setuptools')
license=('MIT')
arch=('x86_64')
source=("https://pypi.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz"
    "LICENSE")
sha256sums=('6de1b16e70335b7bde266707eb401a3aaec220fb66c5d13b02abf0eab8be782b'
            '023e92e0b6343c75e6912abd5076ea28b520ca27d1cd44497bcf363b9b8f559d')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build_ext --inplace
    pytest
}

package() {
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
