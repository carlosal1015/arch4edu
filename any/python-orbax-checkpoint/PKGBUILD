# Maintainer: Daniel Bershatsky <bepshatsky@yandex.ru>
pkgname=python-orbax-checkpoint
_pkgname=${pkgname#python-}
pkgver=0.5.20
pkgrel=1
pkgdesc='Orbax provides common utility libraries for JAX users (checkpoint).'
arch=('any')
url='https://github.com/google/orbax'
license=('Apache')
depends=(
    'python-absl'
    'python-etils'
    'python-importlib_resources'  # etils[epath]
    'python-jax'
    'python-msgpack'
    'python-nest-asyncio'
    'python-numpy'
    'python-protobuf'
    'python-tensorstore'
    'python-typing_extensions'  # etils[epy]
    'python-yaml'

)
makedepends=('python-build' 'python-flit-core' 'python-installer')
optdepends=(
    'python-orbax-export: Serialization JAX to TensorFlow Saved Models.'
)
conflicts=('python-orbax')
replaces=('python-orbax')
source_part='b2/bf'
source_hash='e338db8c4761b7389504c94a7c03e76f747fb82a312c913a5055ec2421a1'
source=("$_pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/$source_part/$source_hash/orbax_checkpoint-$pkgver.tar.gz")
sha256sums=('57dd4175e698a8c4957991ab7e6c19d7b3a8792ae7072b9cd2b267ce5b34884d')

build() {
    python -m build -nw orbax_checkpoint-$pkgver
}

package() {
    python -m installer \
        --compile-bytecode 1 \
        --destdir $pkgdir \
        orbax_checkpoint-$pkgver/dist/orbax_checkpoint-$pkgver-*.whl
}
